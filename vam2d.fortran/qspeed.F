c!#   $Id: qspeed.F,v 1.1 1997/02/10 16:39:08 leidner Exp $
c!#   $Log: qspeed.F,v $
c!#   Revision 1.1  1997/02/10 16:39:08  leidner
c!#   Initial revision
c!#
C ROUTINE          QSPEED FUNCTION  M0004  VAXFORT  QSPEED.FOR
C
C PURPOSE          QUICK RETRIEVAL OF WIND SPEED.
C
C PROJECT          EDIPVS
C
C ARGUMENTS        DG     (ENTRY)  R4  FIRST GUESS WIND DIRECTION (DEG)
C                  S0     (ENTRY)  R4  3 MEASURED SIGMA-0'S (DB)
C                  AI     (ENTRY)  R4  3 BEAM INCIDENCE ANGLES (DEG)
C                  BA     (ENTRY)  R4  3 BEAM AZIMUTH ANGLES (DEG)
C                  QSPEED (FUNC)   R4  QUICK-LOOK WIND SPEED (M/S)
C
C COMMON           /CMODEL/
C                  MODEL  (WRITE)  C10  MODEL ID
C
C FILES            UNIT(12)  FILE(CMOD_QSLUT)  SPEED COEFFICIENTS (I/P)
C
C INTRINSICS       INT, MAX, MIN
C
C DESCRIPTION      PERFORMS A QUICK-LOOK RETRIEVAL OF WIND SPEED
C                  GIVEN A FIRST-GUESS WIND DIRECTION.
C                  RETRIEVAL ERRORS WILL BE DOMINATED BY THE EFFECTIVE
C                  NOISE IN THE SIGMA-0 MEASUREMENTS; NOISLESS S-0
C                  LEAD TO ERRORS (S.D.) OF GENERALLY <0.1M/S, BUT
C                  DEPEND ON THE C-BAND MODEL.
C                  SPEED COEFFICIENTS FILE IS SPECIFIED VIA THE LOGICAL
C                  NAME 'CMOD_QSLUT'.
C
C ALGORITHM        USES A PRE-COMPUTED SET OF QUADRATIC COEFFICIENTS
C                  THAT GIVE LOG(SPEED) OR SQRT(SPEED) AS A FUNCTION 
C                  OF BACKSCATTER IN DB, TABULATED WITH BEAM INCIDENCE 
C                  ANGLE AND WIND DIRECTION RELATIVE TO THE BEAM AZIMUTH.
C                  THE TABULATED SET OF COEFFICIENTS ARE INTERPOLATED
C                  TO GIVEN INCIDENCE ANGLE AND WIND DIRECTION, AND
C                  APPLIED TO THE MEASURED SIGMA-0 GIVE LOG(SPEED)
C                  OR SQRT(SPEED) FOR EACH BEAM AND HENCE MEAN SPEED
C                  FROM ALL USABLE BEAMS - MISSING BEAMS ARE ALLOWED
C                  FOR. 
C
C REFERENCE        COEFFS. GENERATED BY PROGRAM LUTGEN (M1000) AND
C                  TESTED BY SPETEST (M3000).
C
C VERSION          1.04  NOVEMBER 1992  ORIG: D.OFFILER  APR 89
C
C CHANGES          01  COEFFICIENTS FILE VIA LOGICAL NAME.  D.O.
C                  02  LIMIT QSPEED VALUE TO 100M/S         D.O.  JAN 92
C                  03  (A) MODIFIED LUT FILE (15-60DEG INC.) AND
C                          COEFFS FOR TWO SPEED (S0) RANGES.
C                      (B) IF MODEL IS CMOD5, USE SQRT(V) RELATION,
C                          ELSE LOG(V).
C                      (C) INTERPOLATE COEFFS.
C                      (D) NO LONGER USE KP AS SCALING.     D.O.  OCT 92
C                  04  USE S0 VALUE TO REJECT MISSING BEAMS INSTEAD OF
C                      LOGICAL FLAG ARRAY.                  D.O.  NOV 92
C
c
c Recieved by P.WOICESHYN (JPL/NOAA-NMC) from Dave OFFILER, UKMO, Bracknell, UK
c	for use and modification as part of the ESA calibration
c	and validation team activity -- FEB 18, 1993.
c
c	Transfer of this code to other users is not permitted without receiving
c	prior permission from Woiceshyn and Offiler.
c
c	Errors and modifications of this code are to be reported to Woiceshyn.
c	
c
c
      FUNCTION QSPEED ( DG, S0, AI, BA )

      IMPLICIT NONE

      integer ioopen,IPCHAN
      INTEGER NB, NA, ND, NV
      real    FD, FA, DD, DA, S0MIN
      PARAMETER ( IPCHAN = 42, NB = 3, ND = 37, NA = 46, NV = 2 )  
      PARAMETER ( FD = 0.0, FA = 15.0 )
      PARAMETER ( DD = 5.0, DA =  1.0 )
      PARAMETER ( S0MIN = -50.0 )

* ARGUMENT LIST PARAMETERS

      real     QSPEED, DG, S0(NB), AI(NB), BA(NB)

* LOCAL PARAMETERS

      CHARACTER*10 MODEL, LUTFIL
      INTEGER IA, IB, ID, IV
      real    B0, B1, B2
      real    C0(NV,ND,NA), C1(NV,ND,NA), C2(NV,ND,NA), SC(ND,NA)
      real    A, A0, D, D0, EN, R1, R2, VV, W, X, Y, Z
      LOGICAL FIRST

      COMMON / CMODEL / MODEL

      DATA LUTFIL / 'CMOD_QSLUT' /
      DATA FIRST  / .TRUE. /
                                                              
* READ SPEED COEFFICIENTS LOOK-UP TABLE FOR FIRST ENTRY.

	save
	
      IF ( FIRST ) THEN
          if (ioopen(IPCHAN) .ne. 0) then 
             PRINT *, '*** QSPEED: Unable to open file'
             CALL EXIT(1)
          ENDIF
         READ  ( IPCHAN ) MODEL, C0, C1, C2, SC
         FIRST = .FALSE.
         call ioclose(IPCHAN)
        ENDIF

* FIND NEAREST TABULATED COEFFS & EVALUATE SPEED FROM EACH ACTIVE BEAM
* & CALCULATE MEAN
                     
      VV = 0.0
      EN = 0.0
      DO 30 IB = 1, NB
         IF ( S0(IB) .GT. S0MIN ) THEN
            EN = EN + 1.0

* D IS RELATIVE WIND DIRECTION TO BEAM (0-180DEG ONLY)

            D = BA(IB) - DG
   10       CONTINUE
            IF ( D .LT. 0.0 ) D = -D                     
               IF ( D .LE. 180.0 ) GOTO 20
               D = D - 360.0
               GOTO 10
   20       CONTINUE
            A = AI(IB)

* INTERPOLATE COEFFICIENTS IN REL.DIR. & INC.ANG., AND SELECT APPROPRIATE
* SET OF COEFFS WITH NOMINAL WIND SPEED RANGE.

            D0 = ( D - FD ) / DD + 1.0
            A0 = ( A - FA ) / DA + 1.0

            ID = MIN ( MAX ( INT ( D0 ), 1 ), ND-1 )
            IA = MIN ( MAX ( INT ( A0 ), 1 ), NA-1 )
            IF ( S0(IB) .LT. SC(ID,IA) ) THEN
               IV = 1
            ELSE
               IV = 2
            ENDIF

            R1 = D0 - ID
            R2 = A0 - IA

            W = C0(IV,ID,IA)
            X = C0(IV,ID,IA+1)   - W
            Y = C0(IV,ID+1,IA)   - W
            Z = C0(IV,ID+1,IA+1) - W - X - Y
            B0 = W + X * R2 + Y * R1 + Z * R1 * R2

            W = C1(IV,ID,IA)
            X = C1(IV,ID,IA+1)   - W
            Y = C1(IV,ID+1,IA)   - W
            Z = C1(IV,ID+1,IA+1) - W - X - Y
            B1 = W + X * R2 + Y * R1 + Z * R1 * R2
 
            W = C2(IV,ID,IA)
            X = C2(IV,ID,IA+1)   - W
            Y = C2(IV,ID+1,IA)   - W
            Z = C2(IV,ID+1,IA+1) - W - X - Y
            B2 = W + X * R2 + Y * R1 + Z * R1 * R2

            VV = VV +
     $           B0 + B1 * S0(IB) + B2 * S0(IB) * S0(IB)
         ENDIF
   30 CONTINUE
                                                          
* ESTIMATE SPEED OVER ALL USABLE BEAMS
                
      IF ( EN .GT. 0.5 ) THEN
         VV = VV / EN
         IF ( MODEL(5:5) .EQ. '5' ) THEN
            QSPEED = MIN ( VV * VV, 100.0 )
         ELSE
            QSPEED = 10.0 ** MIN ( VV, 2.0 )
         ENDIF
      ELSE
         QSPEED = 0.0
      ENDIF

      END
